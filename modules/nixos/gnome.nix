{
  pkgs,
  lib,
  config,
  ...
}:
let
  mkCatppuccinGtk =
    {
      tweaks ? [ ],
    }:
    pkgs.unstable.catppuccin-gtk.override {
      accents = [ config.catppuccin.accent ];
      variant = config.catppuccin.flavor;
      size = "compact";
      inherit tweaks;
    };
in
{

  options.euvlok.nixos.gnome.enable = lib.options.mkEnableOption "GNOME";

  config = lib.modules.mkIf config.euvlok.nixos.gnome.enable {
    euvlok.nixos.gui.enable = lib.modules.mkDefault true;

    services = {
      displayManager.gdm.enable = true;
      desktopManager.gnome.enable = true;
      gnome = {
        glib-networking.enable = true;
        gnome-browser-connector.enable = true;
        gnome-online-accounts.enable = true;
        gnome-remote-desktop.enable = true;
        sushi.enable = true;
      };
    };

    environment = {
      systemPackages =
        builtins.attrValues {
          inherit (pkgs.unstable)
            apostrophe # Markdown Editor
            decibels # Audio Player
            gnome-obfuscate # Censor Private Info
            loupe # Image Viewer
            mousai # Shazam-like
            resources # Task Manager
            textpieces
            ;
          inherit (pkgs.unstable.gnomeExtensions) appindicator clipboard-indicator;
        }
        ++ lib.lists.optionals config.catppuccin.enable [
          (mkCatppuccinGtk { tweaks = [ "normal" ]; })
        ];

      gnome.excludePackages = builtins.attrValues {
        inherit (pkgs.unstable)
          epiphany # Browser
          evince # Docs
          geary # Email
          # gnome-builder
          gnome-console
          # gnome-maps
          gnome-music
          gnome-tour
          # gnome-weather
          ;
      };
    };
  };
}
