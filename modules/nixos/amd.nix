{
  pkgs,
  lib,
  config,
  ...
}:
{
  _class = "nixos";
  _file = ./amd.nix;
  key = toString ./amd.nix;
  options.nixos.amd.enable = lib.options.mkEnableOption "AMD drivers";

  config = lib.modules.mkMerge [
    (lib.modules.mkIf config.nixos.amd.enable {
      hardware.graphics.extraPackages = builtins.attrValues {
        inherit (pkgs) clinfo;
        inherit (pkgs.rocmPackages.clr) icd;
      };
      environment.systemPackages = builtins.attrValues { inherit (pkgs) lact; };
      systemd = {
        packages = builtins.attrValues { inherit (pkgs) lact; };
        services.lactd.wantedBy = [ "multi-user.target" ];
        tmpfiles.rules =
          let
            rocmEnv = pkgs.symlinkJoin {
              name = "rocm-combined";
              paths = builtins.attrValues {
                inherit (pkgs.rocmPackages) rocblas hipblas clr;
              };
            };
          in
          [ "L+    /opt/rocm   -    -    -     -    ${rocmEnv}" ];
      };
    })
  ];
}
