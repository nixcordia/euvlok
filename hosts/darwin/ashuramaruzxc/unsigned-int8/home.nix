{
  homeManagerModule,
  personalModule,
  sharedModule,
}:
{ pkgs, ... }:
let
  commonImports = [
    { home.stateVersion = "26.05"; }
    ../../../hm/ashuramaruzxc/aliases.nix
    ../../../hm/ashuramaruzxc/git.nix
    ../../../hm/ashuramaruzxc/helix.nix
    ../../../hm/ashuramaruzxc/ssh.nix
    ../../../hm/ashuramaruzxc/starship.nix
  ];

  catppuccinConfig = {
    catppuccin = {
      enable = true;
      flavor = "mocha";
      accent = "flamingo";
    };
  };

  hmModuleConfig = [
    {
      euvlok.home = {
        codex.enable = true;
        fastfetch.enable = true;
        firefox = {
          zen-browser.enable = true;
          defaultSearchEngine = "kagi";
        };
        ghostty.enable = true;
        helix.enable = true;
        mpv.enable = true;
        nh.enable = true;
        nixcord.enable = true;
        vscode.enable = true;
        zed-editor.enable = true;
        languages = {
          # cpp.enable = true;
          csharp = {
            enable = true;
            version = "10";
          };
          go.enable = true;
          haskell.enable = true;
          java = {
            enable = true;
            version = "21";
          };
          javascript.enable = true;
          kotlin.enable = true;
          lua.enable = true;
          python.enable = true;
          ruby.enable = true;
          rust.enable = true;
          scala.enable = true;
        };
      };
    }
  ];

  sopsConfig = [
    { sops.defaultSopsFile = ../../../../secrets/ashuramaruzxc_unsigned-int32.yaml; }
  ];

  macosPackages = builtins.attrValues {
    inherit (pkgs.unstable)
      alt-tab-macos
      betterdisplay
      ice-bar
      iina
      keka
      raycast
      shottr
      stats
      the-unarchiver
      ;
    inherit (pkgs.eupkgs) aldente;
  };

  socialPackages = builtins.attrValues {
    inherit (pkgs) signal-desktop materialgram;
  };

  multimediaPackages = builtins.attrValues {
    inherit (pkgs)
      # nicotine-plus
      qbittorrent
      ;
  };
  productivityPackages = builtins.attrValues {
    inherit (pkgs)
      anki-bin
      inkscape
      audacity
      ;
    inherit (pkgs.eupkgs) helium-browser;
  };

  authPackages = builtins.attrValues {
    inherit (pkgs)
      bitwarden-desktop
      keepassxc
      yubikey-manager
      ;
  };

  gamingPackages = builtins.attrValues {
    inherit (pkgs.unstable)
      chiaki
      osu-lazer-bin
      prismlauncher
      ryubing
      # winetricks
      xemu
      ;
    inherit (pkgs.jetbrains) dataspell datagrip;
  };

  jetbrainsPackages =
    let
      inherit (pkgs.unstable.jetbrains) rider clion idea;
      # inherit (pkgs.jetbrains.plugins) addPlugins;
      # commonPlugins = [
      #   "better-direnv"
      #   "catppuccin-icons"
      #   "catppuccin-theme"
      #   "csv-editor"
      #   "ini"
      #   "nixidea"
      #   "rainbow-brackets"
      # ];
    in
    [
      rider
      clion
      idea
    ];
  # builtins.attrValues {
  #   riderWithPlugins = addPlugins rider (commonPlugins ++ [ "python-community-edition" ]);
  #   clionWithPlugins = addPlugins clion (commonPlugins ++ [ "rust" ]);
  #   ideaUltimateWithPlugins = addPlugins idea-ultimate (
  #     commonPlugins ++ [ "go" "minecraft-development" "python" "rust" "scala" ]
  #   );
  # };

  allPackages =
    macosPackages
    ++ socialPackages
    ++ multimediaPackages
    ++ authPackages
    ++ productivityPackages
    ++ gamingPackages
    ++ jetbrainsPackages;

  userExtras = [
    { home.packages = allPackages; }
    {
      programs = {
        btop.enable = true;
        rbw = {
          enable = true;
          settings = {
            email = "ashuramaru@tenjin-dk.com";
            base_url = "bitwarden.tenjin-dk.com";
            lock_timeout = 600;
            pinentry = pkgs.pinentry_mac;
          };
        };
      };
    }
  ];

  ashuramaru = commonImports ++ [ catppuccinConfig ] ++ sopsConfig ++ hmModuleConfig ++ userExtras;
in
{
  _class = "darwin";
  _file = ./home.nix;
  key = toString ./home.nix;
  imports = [ homeManagerModule ];

  home-manager = {
    useUserPackages = true;
    useGlobalPkgs = true;
    backupFileExtension = "bak";
    sharedModules = [
      sharedModule
      personalModule
    ];
    users.ashuramaru.imports = ashuramaru;
  };
}
